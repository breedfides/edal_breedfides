plugins {
  id "com.github.johnrengelman.shadow" version "5.0.0"
  id "com.github.hierynomus.license" version "0.14.0"
  id "net.researchgate.release" version "2.6.0"
  id "io.codearte.nexus-staging" version "0.21.2"
  id "org.openjfx.javafxplugin" version "0.0.9"
}

project.ext {
    aspectjVersion = '1.9.5'
    jerseyVersion = '2.27'
    jettyVersion = '9.4.32.v20200930'
    junitVersion = '5.6.0'
}

apply plugin: 'eclipse'
apply plugin: 'net.researchgate.release'
apply plugin: 'io.codearte.nexus-staging'

allprojects {

	apply plugin:'java'
	apply plugin:'maven'
	apply plugin:'signing'
	apply plugin:'com.github.johnrengelman.shadow'
	apply plugin:'com.github.hierynomus.license'
	apply plugin:'org.openjfx.javafxplugin'
	
	repositories {
		maven {url 'https://www.jitpack.io' 
		 	content {
        		includeGroupByRegex "com\\.github\\.develar.*"
        		//includeGroupByRegex "com\\.github\\.h2database.*"
    		}	
    	}
	    mavenCentral()
	    jcenter()
	    maven {url "https://oss.sonatype.org/content/repositories/snapshots"}
	    maven {url "http://repo.maven.apache.org/maven2"}
	    maven {url "https://maven.eveoh.nl/content/repositories/releases"}
        maven {url "https://plugins.gradle.org/m2/"}
	}
	
	sourceCompatibility = JavaVersion.VERSION_12
	targetCompatibility = JavaVersion.VERSION_12
	
	javafx {
		version ='12'
	    modules = ['javafx.base','javafx.controls','javafx.web','javafx.swing','javafx.graphics','javafx.fxml','javafx.media']
	}
	
	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}
	
	javadoc {
    	options.addBooleanOption('html5', true)
	}
	
	test {
        useJUnitPlatform()
      	systemProperties['junit.jupiter.execution.parallel.enabled'] = true

  		if(Runtime.runtime.availableProcessors()>=4){
	    	maxParallelForks = Runtime.runtime.availableProcessors()-2
	    }
	    else{
	    	maxParallelForks = Runtime.runtime.availableProcessors()
	    }

	    minHeapSize = "8g"
	    maxHeapSize = "16g"
	
	    systemProperties = ["java.security.policy": file("./src/main/resources/de/ipk_gatersleben/bit/bi/edal/primary_data/policy.txt").absolutePath ]
	
	    testLogging.showStandardStreams = true
	    testLogging.exceptionFormat = 'full'
		
		exclude 'de/ipk_gatersleben/bit/bi/edal/test/**'
		exclude 'de/ipk_gatersleben/bit/bi/edal/helper/**'
		exclude 'de/ipk_gatersleben/bit/bi/edal/primary_data/rmi/client/helper/**'
		exclude 'de/ipk_gatersleben/bit/bi/edal/rmi/test/**'
		exclude 'de/ipk_gatersleben/bit/bi/edal/primary_data/rmi/client/test/ClientServerTest*'
		exclude 'de/ipk_gatersleben/bit/bi/edal/primary_data/rmi/client/test/parallel/RmiUploadDownloadOne*'
		exclude 'de/ipk_gatersleben/bit/bi/edal/primary_data/rmi/client/test/parallel/RmiUploadDownloadTwo*'
		
	}
	
	license {
    header rootProject.file('src/main/resources/license.txt')
    strictCheck false
 	ext.year = Calendar.getInstance().get(Calendar.YEAR)
	include "**/*.java"
	exclude "**/*package-info.java"
	}
	
	sourceSets {
        main {
            java {srcDirs=["src/main/java"]}
            resources {srcDirs=["src/main/resources"]}
        }
	}
	
	task javadocJar(type: Jar) {
	 	classifier = 'javadoc'
	    from javadoc
	}

	task sourcesJar(type: Jar) {
	    classifier = 'sources'
	    from sourceSets.main.allSource
	}
	
	shadowJar {
   		classifier = 'jar-with-dependencies'
		zip64 true
	}
	
	artifacts {
   		archives javadocJar, sourcesJar
	}

	if(!project.hasProperty('signing.keyId') || !project.hasProperty('signing.password')|| !project.hasProperty('signing.secretKeyRingFile')){   
		println "WARNING: If you want to sign your archives please provide the 'signing.keyId' and 'signing.password' and 'signing.secretKeyRingFile' properties"
	}
	else{
		signing {
		    sign configurations.archives
		}
	}

}



subprojects {
	uploadArchives {
		repositories {
			mavenDeployer {
				if(!project.hasProperty('nexusUsername') || !project.hasProperty('nexusPassword')){   
	            	println "WARNING: If you want to release to Sonatype please provide the 'nexusUsername' and 'nexusPassword' properties"
				}
				else{
				beforeDeployment { 
						MavenDeployment deployment -> signing.signPom(deployment) 
					}
					repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
						authentication(userName: nexusUsername, password: nexusPassword)
						if(project.hasProperty('systemProp.https.proxyHost') && project.hasProperty('systemProp.https.proxyPort')){
							println "INFO: Using system proxy for upload : "+System.properties['https.proxyHost']+":"+System.properties['https.proxyPort']
							proxy(host: System.properties['https.proxyHost'], port: System.properties['https.proxyPort'] as Integer, type: 'http')
						}
					}
					snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
						authentication(userName: nexusUsername, password: nexusPassword)
						if(project.hasProperty('systemProp.https.proxyHost') && project.hasProperty('systemProp.https.proxyPort')){
							println "INFO: Using system proxy for upload : "+System.properties['https.proxyHost']+":"+System.properties['https.proxyPort']
							proxy(host: System.properties['https.proxyHost'], port: System.properties['https.proxyPort'] as Integer, type: 'http')
						}					
					}
				}
				pom.project {
		        name = projectname
		        packaging = 'jar'
		        artifactId = projectname
		        description = projectdescription
		        url = projecturl
	
		        scm {
		          	connection 'scm:git:https://bitbucket.org/ipk_bit_team/electronicdataarchivelibrary.git'
		          	developerConnection 'scm:git:https://bitbucket.org/ipk_bit_team/electronicdataarchivelibrary.git'
		          	url 'https://bitbucket.org/ipk_bit_team/electronicdataarchivelibrary.git'
		        }
	
		        licenses {
		          	license {
		            	name 'GNU General Public License (GPL) Version 3'
		            	url 'https://www.gnu.org/licenses/gpl-3.0.html'
		          	}
		        }
	
		        developers {
			          developer {
			            name 'Daniel Arend'
			            email 'arendd@ipk-gatersleben.de'
			            organization = 'IPK Gatersleben'
			            organizationUrl 'http://www.ipk.gatersleben.de'
			          }
			          developer {
			            name 'Matthias Lange'
			            email 'lange@ipk-gatersleben.de'
			            organization = 'IPK Gatersleben'
			            organizationUrl 'http://www.ipk.gatersleben.de'
			          }
		        }
	      	}
			}
		}
		
	}
	uploadArchives.dependsOn test
}

nexusStaging {
	if(project.hasProperty('nexusUsername') && project.hasProperty('nexusPassword')){
		stagingProfileId  = "de.ipk-gatersleben"	
		username = nexusUsername
		password = nexusPassword
	}
}

release {
    failOnCommitNeeded = false
    failOnPublishNeeded = true
    failOnSnapshotDependencies = false
    failOnUnversionedFiles = true
    failOnUpdateNeeded = true
    revertOnFail = true

  	preCommitText = ''
  	preTagCommitMessage = '[gradle-release-plugin] - prepare release '
    tagCommitMessage = '[gradle-release-plugin] - creating tag: '
    newVersionCommitMessage = '[gradle-release-plugin] - prepare for next development iteration '
    
    tagTemplate = "eDAL-MetaDataAPI-"+'$version'
    versionPropertyFile = 'gradle.properties'
    versionProperties = []
    buildTasks = []
   
    scmAdapters = [
        net.researchgate.release.GitAdapter
    ]

    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
        pushToBranchPrefix = ''
        commitVersionFileOnly = false
        signTag = false
    }
}

project.tasks.afterReleaseBuild.dependsOn project.getSubprojects().collect({it.getTasks().getByName("uploadArchives")})

//API Project specific stuff
project(':eDAL-MetaDataAPI') {	
 	dependencies {
	   
	   
	   
	    compile group: 'commons-io', name: 'commons-io', version:'2.6'
	    compile group: 'org.aspectj', name: 'aspectjrt', version: aspectjVersion
	    compile group: 'com.h2database', name: 'h2', version:'1.4.199'
    	/*compile ('com.github.h2database:h2database:master-SNAPSHOT'){
        	exclude group: 'org.apache.lucene'
    	}*/
		//compile group: 'org.hibernate', name: 'hibernate-search-orm', version:'5.11.5.Final'*/
		compile group: 'org.hibernate.search', name: 'hibernate-search-mapper-orm', version: '6.0.0.Beta11'
    	compile group: 'org.hibernate.search', name: 'hibernate-search-backend-lucene', version: '6.0.0.Beta11'
    	// https://mvnrepository.com/artifact/org.apache.lucene/lucene-highlighter
		compile group: 'org.apache.lucene', name: 'lucene-highlighter', version: '8.7.0'
		// https://mvnrepository.com/artifact/org.apache.lucene/lucene-backward-codecs
		compile group: 'org.apache.lucene', name: 'lucene-backward-codecs', version: '8.7.0'
		// https://mvnrepository.com/artifact/org.apache.lucene/lucene-misc
		compile group: 'org.apache.lucene', name: 'lucene-misc', version: '8.7.0'
		
		
    	
    	
		compile group: 'org.hibernate', name: 'hibernate-core', version: '5.4.22.Final'
		compile group: 'org.hibernate', name: 'hibernate-jcache', version: '5.4.22.Final'
		compile group: 'org.hibernate', name: 'hibernate-c3p0', version: '5.4.22.Final'
	 	compile group: 'org.ehcache', name: 'ehcache', version:'3.6.1'
	
	    compile group: 'org.apache.velocity', name: 'velocity-engine-core', version:'2.0'
	    compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.8.1'
	    compile group: 'com.sun.mail', name: 'javax.mail', version:'1.6.2'
	    compile group: 'org.quartz-scheduler', name: 'quartz', version:'2.3.0'
	    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: jettyVersion
	    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: jettyVersion
	    compile group: 'org.eclipse.jetty.http2', name: 'http2-server', version: jettyVersion
	  	compile group: 'org.eclipse.jetty.http2', name: 'http2-common', version: jettyVersion
	    compile group: 'org.eclipse.jetty', name: 'jetty-alpn-conscrypt-server', version: jettyVersion	    
	    compile group: 'org.eclipse.jetty', name: 'jetty-alpn-java-server', version: jettyVersion	    
	    
	    compile group: 'com.googlecode.json-simple', name: 'json-simple', version:'1.1.1'
	   
	   	compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.16.0'	    
		compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.16.0'
		
	    compile group: 'com.maxmind.geoip2', name: 'geoip2', version:'2.12.0'
	    compile group: 'org.apache.tika', name: 'tika-core', version:'1.19.1'
	    
		compile group: 'org.bidib.com.github.markusbernhardt', name: 'proxy-vole', version: '1.0.10'
				  
	 	compile group: 'com.github.seancfoley', name:'ipaddress', version:'5.2.1'
	  	
	   	   	
	   	testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    	testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
	    	   	
	   	compile group: 'org.glassfish.jersey.connectors', name: 'jersey-apache-connector', version: jerseyVersion
	    compile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: jerseyVersion
	    compile group: 'org.glassfish.jersey.core', name: 'jersey-server', version: jerseyVersion
	    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: jerseyVersion
     	compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-jetty-http', version: jerseyVersion
     	compile group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: jerseyVersion
     	compile group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: jerseyVersion	 
     	compile group: 'org.glassfish.jersey.security', name: 'oauth2-client', version: jerseyVersion

		compile group: 'javax.xml.bind', name: 'jaxb-api', version:'2.3.1'	 
     	compile group: 'com.sun.xml.bind', name: 'jaxb-core', version:'2.3.0.1'	 
     	compile group: 'com.sun.xml.bind', name: 'jaxb-impl', version:'2.3.1'	 
     	compile group: 'javax.activation', name: 'activation', version:'1.1.1'	
     	
	}
	
/*	task shadowJarLogin(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
		group = "shadow"
		classifier = 'jar-with-dependencies'
		description = "eDAL-Login-Test"
		exclude '*.p12'
		
		mergeServiceFiles()
		 
		manifest {
	        attributes 	'Main-Class':'de.ipk_gatersleben.bit.bi.edal.sample.EdalLoginTestKerberos', 'Multi-Release':'true'
	    }
		from(project.convention.getPlugin(JavaPluginConvention).sourceSets.main.output)
		configurations = [project.configurations.runtime]
		exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
	}
*/	
	artifacts{
		archives shadowJar //, shadowJarLogin
	}
}

//Server Project specific stuff
project(':eDAL-MetaDataAPI-Server') {
	dependencies {
		compile project(':eDAL-MetaDataAPI')
	    compile group: 'commons-cli', name: 'commons-cli', version:'1.4'
	   	compile group: 'com.healthmarketscience.rmiio', name: 'rmiio', version:'2.1.2'
	}

	task shadowJarServer(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
		group = "shadow"
		classifier = 'jar-with-dependencies'
		description = "eDAL-Server"
		exclude '*.p12'
		
		mergeServiceFiles()
		 
		manifest {
	        attributes 	'Main-Class':'de.ipk_gatersleben.bit.bi.edal.rmi.server.EdalServer', 'Multi-Release':'true'
	    }
		from(project.convention.getPlugin(JavaPluginConvention).sourceSets.main.output)
		configurations = [project.configurations.runtime]
		exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
	}

	artifacts {
		archives shadowJarServer
	}
}
//Client Project specific stuff
project(':eDAL-MetaDataAPI-Client') {
	dependencies {
    	compile project(':eDAL-MetaDataAPI-Server') 
    	compile group: 'org.swinglabs.swingx', name: 'swingx-all', version:'1.6.4'
    	compile group: 'com.miglayout', name: 'miglayout-core', version:'4.2'
    	compile group: 'com.miglayout', name: 'miglayout-swing', version:'4.2'
    	testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    	testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
    	
    	implementation group: 'org.apache.poi', name: 'poi', version: '5.0.0'
    	implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.0.0'
    	
    	
    }
	
	artifacts{
		archives shadowJar
	}
}